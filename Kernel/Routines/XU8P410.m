XU8P410 ;OAK_BP/BDT - NATIONAL PROVIDER IDENTIFIER; 7/27/06 ;8/22/06  11:32
 ;;8.0;KERNEL;**410**; July 10, 1997;Build 27
 ;;
PRE ; run pre-routine
 K ERR
 Q
 ;
POST ; run post-routine
 D SPRM,RI4,RI200
 N ZTDESC,ZTDTH,ZTIO,ZTRTN
 ; job off task to queue cbo list (can't do it in an install)
 K ^XTMP("NPIVALS")
 S ZTIO="",ZTDTH=$$NOW^XLFDT(),ZTRTN="CBOQUEUE^XUSNPIED",ZTDESC="XUS NPI CBOLIST QUEUEING" D ^%ZTLOAD
 Q
SPRM ; Delete a parameter
 N XUSPR,XUSPCK
 S XUSPCK=$O(^DIC(9.4,"B","KERNEL",0))
 I 'XUSPCK Q
 S XUSPCK=XUSPCK_";DIC(9.4,"
 D EN^XPAR(XUSPCK,"XUSNPI QUALIFIED IDENTIFIER","Pharmacy_ID","@",.ERR)
 Q
RI200 ; Re-index "NPI42" file #200
 N I S I=0
 F  S I=$O(^VA(200,I)) Q:I'>0  D
 . I $D(^VA(200,I,"NPISTATUS"))'>0 Q
 . N DIK,DA S DA(1)=I,DIK="^VA(200,DA(1),""NPISTATUS"",",DIK(1)=".03^NPI42" D ENALL2^DIK
 . N DIK,DA S DA(1)=I,DIK="^VA(200,DA(1),""NPISTATUS"",",DIK(1)=".03^NPI42" D ENALL^DIK
 Q
RI4 ; Re-index "NPI42" file #4
 N I S I=0
 F  S I=$O(^DIC(4,I)) Q:I'>0  D
 . I $D(^DIC(4,I,"NPISTATUS"))'>0 Q
 . N DIK,DA S DA(1)=I,DIK="^DIC(4,DA(1),""NPISTATUS"",",DIK(1)=".03^NPI42" D ENALL2^DIK
 . N DIK,DA S DA(1)=I,DIK="^DIC(4,DA(1),""NPISTATUS"",",DIK(1)=".03^NPI42" D ENALL^DIK
 Q
